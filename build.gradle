import groovy.json.JsonSlurper

plugins {
    id 'idea'
    id 'groovy'
    id 'se.thinkcode.cucumber-runner' version '0.0.11'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.25"
    id 'io.qameta.allure' version '2.11.2'
    id 'com.adarshr.test-logger' version '3.2.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.gebish:geb-core:7.0"
    testImplementation "org.apache.groovy:groovy-json:3.0.17"
    testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:4.29.0"
    testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:4.29.0"
    testImplementation "org.seleniumhq.selenium:selenium-safari-driver:4.29.0"
    testImplementation "org.seleniumhq.selenium:selenium-support:4.2.2"
    testImplementation "io.cucumber:cucumber-groovy:6.10.4"
    testImplementation "io.cucumber:cucumber-junit:7.11.0"
    testImplementation "io.qameta.allure:allure-cucumber7-jvm:2.20.1"
    testImplementation "io.qameta.allure:allure-junit4:2.20.1"
    testImplementation "com.github.automatedowl:allure-environment-writer:1.0.0"
    testImplementation "org.slf4j:slf4j-simple:1.7.36"
}

allure {
    adapter {
        frameworks {
            junit5 {
                autoconfigureListeners.set(false)
            }
        }
    }
}

test {
    finalizedBy generateCucumberReports
    useJUnit {}
    if (System.getProperty("browser") != null) {
        systemProperty "geb.env", System.getProperty("browser")
    }
    if (System.getProperty("browser.option") != null) {
        systemProperty "browser.option", System.getProperty("browser.option")
    }
    if (System.getProperty("env") != null) {
        systemProperty "env", System.getProperty("env")
    }
    systemProperty "cucumber.filter.tags", getTags()
    systemProperties System.properties
}

testlogger {
    showStandardStreams = true
    theme 'mocha'
}

cucumberReports {
    outputDir = file('build/reports/')
    buildId = '0'
    reports = files('build/reports/cucumber.json')
    excludeTags = ["@TestCaseID:(|\\w+)"]
    expandAllSteps = false
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

cucumber {
    main = 'io.cucumber.core.cli.Main'
    glue = 'support'
    tags = getTags()
    plugin = [
            "pretty",
            "json:${reporting.baseDir}/cucumber.json",
            "junit:${reporting.baseDir}/cucumber.xml",
            "html:${reporting.baseDir}/cucumber-report.html",
            "rerun:${reporting.baseDir}/rerun.txt",
            "io.qameta.allure.cucumber7jvm.AllureCucumber7Jvm"
    ]
}

tasks.cucumber {
    finalizedBy generateCucumberReports
    System.setProperty("allure.results.directory", "build/allure-results")
    if (System.getProperty("browser") != null) {
        System.setProperty("geb.env", System.getProperty("browser"))
    }
    if (System.getProperty("browser.option") != null) {
        System.setProperty("browser.option", System.getProperty("browser.option"))
    }
    if (System.getProperty("env") != null) {
        System.setProperty("env", System.getProperty("env"))
    }
}

task runTests() {
    dependsOn assemble, testClasses
    finalizedBy generateCucumberReports
    doLast {
        javaexec {
            mainClass = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            if (System.getProperty("browser") != null) {
                systemProperty "geb.env", System.getProperty("browser")
            }
            if (System.getProperty("browser.option") != null) {
                systemProperty "browser.option", System.getProperty("browser.option")
            }
            if (System.getProperty("env") != null) {
                systemProperty "env", System.getProperty("env")
            }
            systemProperty "cucumber.filter.tags", getTags()
            systemProperty "allure.results.directory", "build/allure-results"
            args = [
                    '--plugin', 'pretty',
                    '--plugin', "json:${reporting.baseDir}/cucumber.json",
                    '--plugin', "junit:${reporting.baseDir}/cucumber.xml",
                    '--plugin', "html:${reporting.baseDir}/cucumber-report.html",
                    '--plugin', "rerun:${reporting.baseDir}/rerun.txt",
                    '--plugin', "io.qameta.allure.cucumber7jvm.AllureCucumber7Jvm",
                    '--glue', "src/test/resources/cucumber/features",
                    '--glue', "support"
            ]
        }
    }
}

String getTags() {
    def jsonFile = file("src/test/groovy/config/config.json")
    def parsedJson = new JsonSlurper().parseText(jsonFile.text)
    String defaultEnvironment = parsedJson.defaultEnvironment
    List<String> environments = parsedJson.environments
    String selectedEnv = System.getProperty("env")
    String tags = ""
    if (selectedEnv == null) {
        tags = "@${defaultEnvironment}"
    } else {
        selectedEnv = selectedEnv.toLowerCase()
        if (environments.contains(selectedEnv)) {
            tags = "@${selectedEnv}"
        } else {
            tags = "@${defaultEnvironment}"
        }
    }
    tags += " and not @disable"
    if (System.getProperty("tags") != null) {
        tags = "(" + tags + ") and (" + System.getProperty("tags") + ")"
    }
    return tags
}