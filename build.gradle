import org.gradle.api.tasks.testing.logging.TestLogEvent


plugins {
    id 'idea'
    id 'groovy'
    id 'se.thinkcode.cucumber-runner' version '0.0.11'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.24"
}

apply from: "cucumber.gradle"

repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
    google()
    //jcenter()
}

dependencies {
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testImplementation "org.codehaus.groovy:groovy-all:3.0.11"
    testImplementation "org.gebish:geb-core:5.1"
    testImplementation "org.seleniumhq.selenium:selenium-server:3.141.59"
    testImplementation "io.cucumber:cucumber-groovy:6.10.4"
    testImplementation "io.cucumber:cucumber-junit:7.3.2"
    testImplementation "org.junit.vintage:junit-vintage-engine:5.8.2"
}

test {
    dependsOn assemble, testClasses, getGeckoDriver, getChromeDriver, getTags
    finalizedBy generateCucumberReports
    systemProperty driversProperty, driversLocations
    if (System.getProperty("browser") != null) {
        systemProperty "geb.env", System.getProperty("browser")
    }
    if (System.getProperty("browser.option") != null) {
        systemProperty "browser.option", System.getProperty("browser.option")
    }
    systemProperty "cucumber.filter.tags", cucumberTags
    useJUnitPlatform()
    systemProperty("cucumber.junit-platform.naming-strategy", "long")
    testLogging {
        events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED
    }
    systemProperties(project.gradle.startParameter.systemPropertiesArgs)
    //ignoreFailures = true
}

cucumberReports {
    outputDir = file('build/reports/')
    buildId = '0'
    reports = files('build/reports/cucumber.json')
    excludeTags = ["@TestCaseID:(|\\w+)"]
    expandAllSteps = false
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

cucumber {
    main = 'io.cucumber.core.cli.Main'
    glue = 'support'
    tags = cucumberTags
    plugin = [
            "pretty",
            "json:build/reports/cucumber.json",
            "junit:build/reports/cucumber.xml",
            "html:build/reports/cucumber-report.html",
            "rerun:build/reports/rerun.txt",
    ]
}

tasks.cucumber {
    dependsOn getGeckoDriver, getChromeDriver, getTags
    finalizedBy generateCucumberReports
    System.setProperty(driversProperty, driversLocations)
    if (System.getProperty("browser") != null) {
        System.setProperty("geb.env", System.getProperty("browser"))
    }
    if (System.getProperty("browser.option") != null) {
        System.setProperty("browser.option", System.getProperty("browser.option"))
    }
}

task runTests() {
    dependsOn assemble, testClasses, getGeckoDriver, getChromeDriver, getTags
    finalizedBy generateCucumberReports
    doLast {
        javaexec {
            mainClass = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            if (System.getProperty("browser") != null) {
                systemProperty "geb.env", System.getProperty("browser")
            }
            if (System.getProperty("browser.option") != null) {
                systemProperty "browser.option", System.getProperty("browser.option")
            }
            systemProperty "cucumber.filter.tags", cucumberTags
            systemProperty driversProperty, driversLocations
            args = [
                    '--plugin', 'pretty',
                    '--plugin', "json:${reporting.baseDir}/cucumber.json",
                    '--plugin', "junit:${reporting.baseDir}/cucumber.xml",
                    '--plugin', "html:${reporting.baseDir}/cucumber-report.html",
                    '--plugin', "rerun:${reporting.baseDir}/rerun.txt",
                    '--glue', "src/test/resources/cucumber/features",
                    '--glue', "support"
            ]
            jvmArgs = ["-Dgeb.env=${selectedBrowser}"]
            //ignoreExitValue = true
        }
    }
}